;;; -*- Mode: Lisp -*-

;;; Project CHOPIN http://chopin.isr.uc.pt

(in-package :chopin-routing)

(defparameter *messages-received* 0)

(defparameter *config* nil)
(defparameter *max-jitter* nil) ; (/ refresh-interval 4)

(defparameter *msg-seq-num* 0) ; wrap-around is 65535
(defparameter *pkt-seq-num* 0) ; same here

(defparameter *base-station-p* nil)

(defparameter *msg-types* '(:base-station-beacon 1 :node-beacon 2 :node-reply 3))
(defparameter *tlv-types* '(:relay 1 :path 2 :link-status 3))
(defparameter *link-status* '(:lost 0 :symmetric 1 :heard 2))

(defparameter *out-buffer* (sb-concurrency:make-queue))
(defparameter *reply-buffer* (sb-concurrency:make-queue))

(defparameter *duplicate-set* (make-hash-table :test 'equal))
(defparameter *link-set* (make-hash-table :test 'equal))
(defparameter *routing-table* (make-hash-table :test 'equal))

;; Object Factories

(defun make-pkt-header ()
  (make-instance 'pkt-header :pkt-seq-num (incf *pkt-seq-num*)))

(defun make-tlv (value &key (tlv-type :relay))
  (make-instance 'tlv :tlv-type (getf *tlv-types* tlv-type) :value (usocket:host-byte-order value)))

(defun make-tlv-block (tlvs)
  "Return a `tlv-block' composed of TLVS. Mid-way serialization to obtain TLVS-LENGTH."
  (let ((buff (userial:make-buffer)))
    (userial:with-buffer buff
      (dolist (entry tlvs)
	(serialize-tlv entry)))
    ;; tlvs-length is number of octets of tlvs
    (make-instance 'tlv-block :tlvs-length (length buff) :tlvs tlvs)))

(defun make-address-block (&key addr-list)
  "Create an instance of `address-block' from addr-list if supplied or *link-set*."
  (unless (and (zerop (hash-table-count *link-set*)) (not addr-list))
    (make-instance 'address-block
		   :addr-list (or addr-list
				  (mapcar #'l-neighbor-iface-addr
					  (alexandria:hash-table-values *link-set*))))))


(defun make-message (&key msg-header tlv-block address-block)
  (make-instance 'message :msg-header msg-header :tlv-block tlv-block :address-block address-block))

(defun make-packet (message)
  (make-instance 'packet :pkt-header (make-pkt-header)
		 :message message))

;;; Message Building

(defun build-tlvs (tlv-values &key (tlv-type :relay))
  "Return a `list' of `tlv' instances, based on TLV-VALUES."
  (loop for value in tlv-values
	collect (make-tlv value :tlv-type tlv-type)))

(defun build-packet (message)
  (userial:with-buffer (userial:make-buffer)
    (userial:serialize :message message)
    ;; (serialize-tlv-block tlv-block)
    ;; msg-size is size of message including msg-header, that is msg-header+tlv-block+(<addr-block><tlv-block>)*
    (setf (msg-size (msg-header message)) (userial:buffer-length))
    (make-packet message)))

(defun generate-message (&key msg-header (msg-type :base-station-beacon) (tlv-type :relay)
			   tlv-block (tlv-values (list (config-host-address *config*))))
  "Enqueue `packet' in *OUT-BUFFER*."
  (let* ((msg-header (or msg-header (make-instance 'msg-header :msg-type msg-type)))
	 (tlvs (unless tlv-block (build-tlvs tlv-values :tlv-type tlv-type)))
	 (tlvblock (or tlv-block (make-tlv-block tlvs))))
    (with-accessors ((orig-addr msg-orig-addr) (hop-count msg-hop-count) (hop-limit msg-hop-limit)) msg-header
      (incf hop-count)
      (decf hop-limit))
    (sb-concurrency:enqueue (build-packet (make-message :msg-header msg-header :tlv-block tlvblock)) *out-buffer*)
    (sb-thread:signal-semaphore *semaphore*)))

(defun generate-node-message (type &optional msg-orig-addr)
  (case type
    (:base-station-beacon (progn (sb-concurrency:enqueue (build-packet
							  (make-message :msg-header (make-instance 'msg-header :msg-type type :msg-hop-limit 1)
									:address-block (make-address-block :addr-list (list (usocket:host-byte-order (config-host-address *config*)))))) *out-buffer*)
				 (sb-thread:signal-semaphore *semaphore*)))
    (:node-beacon (progn (sb-concurrency:enqueue (build-packet
						  (make-message :msg-header (make-instance 'msg-header :msg-type type :msg-hop-limit 1)
								:address-block (make-address-block))) *out-buffer*)
			 (sb-thread:signal-semaphore *semaphore*)))
    (:node-reply (progn
		     (sb-concurrency:enqueue (make-reply-struct :packet (build-packet
									 (make-message :msg-header (make-instance 'msg-header :msg-type type :msg-orig-addr msg-orig-addr)
										       :address-block (make-address-block :addr-list (list (usocket:host-byte-order (config-host-address *config*))))))
								:destination msg-orig-addr) *reply-buffer*)
		     (sb-thread:signal-semaphore *reply-semaphore*)))))

(defun forward-node-reply (msg-header address-block)
  (let* ((curr-path (addr-list-from-addr-block address-block))
	 (addr-block (make-address-block :addr-list (adjoin (msg-orig-addr msg-header) curr-path))))
    (sb-concurrency:enqueue (make-reply-struct :packet (build-packet (make-message :msg-header (make-instance 'msg-header :msg-type :node-reply)
										   :address-block addr-block))
					       :destination (msg-orig-addr msg-header)) *reply-buffer*))
  (sb-thread:signal-semaphore *semaphore*))

(defun generate-base-station-beacon (message)
  (with-slots (msg-header address-block) message
    (sb-concurrency:enqueue
     (build-packet (make-message :msg-header (make-instance 'msg-header :msg-hop-count (1+ (msg-hop-count msg-header))
							    :msg-orig-addr (usocket:host-byte-order (config-host-address *config*)) :msg-type :base-station-beacon)
				 :address-block (make-address-block :addr-list (adjoin (msg-orig-addr msg-header) (addr-list-from-addr-block address-block))))) *out-buffer*))
  (sb-thread:signal-semaphore *semaphore*))

(defun new-beacon (msg-type)
  "Enqueue a beacon in *OUT-BUFFER* given `msg-type'."
  (assert (valid-msg-type-p msg-type) ()
	  "Invalid message type.")
  (case msg-type
    (:node-beacon (generate-node-message :node-beacon))
    (:base-station-beacon (generate-node-message :base-station-beacon))))

(defun message-hash (&rest rest)
  "Generate hash key based on passed arguments."
  (ironclad:byte-array-to-hex-string
   (ironclad:digest-sequence :sha1 (ironclad:ascii-string-to-byte-array (format nil "狺蝈篝┅┅换序镢弩箝铉ㄤ彐躅汨邈氕漉痨殂狒瀛箦眢绛豉疱矧殓徜潋箦癍铛愆⒁弭躜殒恼刑擅猎怒优元泔铘衢铙犷孱趄骘陀黔再信犷弦汕聊囊萧桢蝼轶瀣蝈趱蝾紊坍ㄧ弭栳箬礤篌徵瀛栳箬眢绛豉疱矧殓徜潋箦癍铛愆漉痨殂狒瀛箦舄┅ㄤ彐躅扉铍箦舡疳蜥眢ī鲠祯弩ㄣ镱骈绛蝈骝弩璀轭翦蝣犰泔铈殓ㄣ镱骈绛铄殓桠栾熹糸礤泔铈殓┅ㄤ彐躅躔溽翦扉铍箦礤篌徵濠⒘滗矧躔溽翦躺嗡优元孱趄骑犷屮轶糸铉孱趄躔溽翦汰陨团萧桢蝼轶瀣泸遽翦铄囔轭氕趱痨濮眭祠轲戾鲠祯瀛忾钿蝈姝轭翦蝣犰铄殓桠栾熹轭绌扉铍箦舡疳蜥眢戾舄è矧殓徜潋眢绛矧殓徜潋眢绛桢徜弪礤篌徵濠┅飙糸礤ㄤ艉箦泔钿ㄤ艉铒鳗í铄殓桠栾熹轭蝈姝轭翦蝣犰┅祗栳箬礤篌徵瀛栳箬矧殓徜潋┅ㄣ躜蝈铘扉铍ㄧ弭栳箬祗栳箬扉铍箦舄┅ㄩㄡ钿沲蝌孱舡扉铍ㄥ聃犰飙铄殓桠矧殒徙瀛徜潋沲蝌孱舡扉铍矧殓徜潋┅痱镧箦翩飙糸礤沲蝌孱舡扉铍飙糸礤麒孱矧眢绛豉疱眢绛桢徜弪礤篌徵濠ㄧ弭眢绛豉疱螵衡狍瀛篝狒轱瞽忮徙镱┅ㄡ钿ㄡ滗蝈篌忪镢礤篌徵濠ㄡ滗颦轭徜潋忪镢氕ㄡ滗蝈篌忪镢礤篌徵濠躞镢脲艉栾篝怡翦矧溴ㄣ镱骈绛栾篝徜潋弩泔铈殓┅┅箦翩飙篝狒躞沲蝌孱舡扉铍ㄧ弭扉铍篝狒躞后眄弭蜷悌躔溽翦蝻豸轭绛翎忪眢绛桢徜弪礤篌徵濠┅箦翩ㄧ弭栳箬祗栳箬扉铍箦舄磲脲轭篝犷沐ъ轭氕趱痨侯彘玷怙颦殒徙瀛徜潋矧殓徜潋呼轫飙糸礤┅┅┅ㄤ彐躅徜潋轭徜潋忪镢氕ㄡ滗蝈篌忪镢矧殓徜潋鏖翳箪雉ㄨ遽黹洎徜潋弩蟓忪镢礤礅弪矧殓徜潋ㄡ滗颦扉篝骝镯徜潋忪镢徜潋弩蟓忪镢氅┅ㄤ彐躅躔溽翦漉痨殂狒瀛箦礤篌徵濠⒚蝈狒噤躔扉汜翦趱痨濮骝镯陀黔扰聊乓麸忮徜溴麸恼刑擅猎怒优元鏖翳箪雉眢绛豉疱眢绛矧殓徜潋眢绛箦癍铛愆眢绛桢徜弪礤篌徵濠箦翩ㄧ弭栳箬礤篌徵瀛栳箬眢绛豉疱眢绛矧殓徜潋眢绛箦癍铛愆漉痨殂狒瀛箦舄磲脲轭篝犷沐т躔扉汜翦趱痨猴蜷绛徜潋眢绛矧殓徜潋喉箸豉疱眢绛豉疱后羼铛眢绛箦癍铛哄瓠糸礤ㄤ艉箦泔钿ㄤ艉铒鳗ㄣ镱骈绛漉瓠栾熹糸礤泔铈殓┅┅┅ㄤ彐躅躔溽翦脲蝾屐蝻豸轭绛翎忪ㄤ弩糸钺糸镱玑翦麽殒徙礤趄殂⒚犰聊沫蚁赵骘蝈殓骢钽糸镱麸躔溽翦嫌蝻豸轭翎忪瀹－溽蝼轭ㄡ滗蝻豸躞镢脲艉桠锃麸滹趑邃聃徜溴篝轭狒轱瞟躞镢脲艉桠锃麸滹趑邃聃徜玑翦麽殒徙礤趄殂┅ㄤ彐躅铄舡栾眢绛桢徜弪⒛弭弪黹铄铄舡栾甬婶彘翳弪骘溟蝈泗扉铍矧囗箸矧殓徜潋骘蝈盹翦镱弩ㄣ镱è犷痨躞眢绛栾瓠泔躅眢绛桢徜弪┅眢绛豉疱眢绛桢徜弪ㄧ弭眢绛豉疱螵衡狍瀛篝狒轱瞽忮徙镱┅眢绛矧殓徜潋眢绛桢徜弪┅癌┅ㄤ彐躅躔溽翦蝻豸轭绛翎忪眢绛桢徜弪⒚蝈狒囹舡孱趄犷徜麸蚁赵晌黔粤绿弄呐釉晌猎上轶翳灬篝镦翳蕴鲠祯弩轭蕴汁绿厦水鏖翳箪雉眢绛矧殓徜潋眢绛箦癍铛眢绛栾瓠泔躅舂眢绛桢徜弪戾舄è溴篝轭狒轱眢绛矧殓徜潋眢绛桢徜弪┅铄舡栾铄舡栾眢绛桢徜弪┅箦翩ㄧ弭栳箬礤篌徵瀛栳箬溴篝轭狒轱瞟蝻豸轭绛翎忪濯磲脲螋孱趄轰弩糸钺糸镱躞镢脲艉桠锃麸滹趑邃聃徜溴篝轭狒轱瞟侯屮舡栾铄舡栾鸿镳泔躅ū眢绛栾瓠泔躅舂后羼铛眢绛箦癍铛愆躔溽翦脲蝾屐蝻豸轭绛翎忪溴篝轭狒轱铄舡栾ㄣ镱骈绛轭翦蜴徙泔铈殓ū眢绛栾瓠泔躅舂┅┅ㄤ彐躅溴飙蝻豸轭绛翎忪ㄤ弩糸钺糸镱戾舄è螋栳箬礤篌徵瀛栳箬躞镢脲艉栾篝怡翦矧溴溴篝轭狒轱瞟┅螋孱趄ㄧ弭栳箬螋栳箬蝻豸轭绛翎忪濯┅蝈龛狍螋栳箬蝻豸轭绛翎忪濯－溽蝼轭ㄤ屐蝻豸螋孱趄溴篝轭狒轱螋孱趄阿ㄣ镱骈绛轭翦蜴徙泔铈殓螋孱趄栾瓠泔躅螋孱趄┅┅ㄤ彐躅鲠扉洵綮霏忪镢氕綮霏忪镢氅⒁弭躜紊殒圄祧忪镢毵泔铘衢铙轭鲠扉綮鲶令轭鲠扉綮泔铘衢铙翳沲蝌孱铒溴徜潋弩矧爱爱爱爱戾è綮鲶磲疸狎＇灬礅溽鲠祯┅綮綮霏忪镢氅┅ㄡ钿铒翎铢＇弪镳綮鲶铒翎铢＇栾篝徜潋弩蟓綮鲶┅┅ㄤ彐躅簌眄弭蜷悱扉铍眢绛矧殓徜潋⒚桢汶殒囗箸矧殓徜潋轶簌眄弭蜷扉铍戾è扉铍ㄧ弭栳箬礤篌徵瀛栳箬眢绛矧殓徜潋扉铍箦舄┅麒孱扉铍ㄧ弭扉铍篝狒躞后眄弭蜷悌飙篝狒躞扉铍┅┅ㄤ彐躅铄殓桠秕颦眢绛矧殓徜潋⒚桢汶殒囗箸矧殓徜潋忮祜铉麸铄殓桠秕蝻豸瀹戾è蝻豸ㄧ弭栳箬礤篌徵瀛栳箬眢绛矧殓徜潋蝻豸轭绛翎忪濯┅ㄡ钿蝻豸螋孱趄栾瓠泔躅蝻豸濠暴┅ㄤ彐躅痱镢弩蟓礤篌徵礤篌徵濠⒄痄狒蚁赵晌黔粤绿弄恼刑擅猎怒优元犷躺嗡优元涉陀黔再信轶郝劣怒釉猎上苇屡撩衔怛镝溷狍舢涉陀黔再信轶何夏怒屡撩衔躅殂狍麸铄舡栾麸箩箦郁狒轱町戾è眢绛桢徜弪眢绛桢徜弪礤篌徵濠ㄡ滗蝈篌忪镢ㄡ滗蝈篌忪镢礤篌徵濠┅鏖翳箪雉眢绛豉疱眢绛矧殓徜潋眢绛栾瓠泔躅舂眢绛桢徜弪ㄩ钽礤篌徵弩蝈沐轹邃ㄣ镱è犷眢绛豉疱ㄧ弭眢绛豉疱螵衡狍瀛篝狒轱瞽忮徙镱┅铒忉箦篝狒轱瞽皙┅躅戾篌ㄡ滗颦轭徜潋忪镢氕徜潋弩蟓忪镢躞镢脲艉栾篝怡翦矧溴ㄣ镱骈绛栾篝徜潋弩泔铈殓┅蜚鲮镧ㄦ矧磲铋⒙劣釉猎上屡撩衔┅蜚鲮镧ㄦ矧磲铋⑨滗忪镢立徜潋弩蟓忪镢氅躔溽翦扉铍箦礤篌徵濠躔溽翦漉痨殂狒瀛箦礤篌徵濠蜚鲮镧ㄦ矧磲铋⒁弭蜥铙黹趑轭怏忮徙镱骝镯廉认杏立躞镢脲艉桠锃麸滹趑邃聃徜眢绛矧殓徜潋眢绛栾瓠泔躅舂ㄧ孱弪狒瀛忉箦篝狒轱瞽忮徙镱礤篌徵濠换麒孱ㄡ钿簌眄弭蜷悱扉铍眢绛矧殓徜潋铒铄殓桠秕颦怏徜潋┅换ㄧ孱弪狒瀛铒溴礤篌徵侯镤瀛蝈痨眢绛矧殓徜潋┅┅è犷眢绛豉疱ㄧ弭眢绛豉疱螵侯镤瀛忮徙镱┅蜚鲮镧ㄦ矧磲铋⑽夏屡撩衔┅躔溽翦扉铍箦礤篌徵濠躔溽翦漉痨殂狒瀛箦礤篌徵濠è犷眢绛豉疱ㄧ弭眢绛豉疱螵侯镤瀛蝈痨┅麒孱箦戾泗轹瀛怛镝溷狍眢绛豉疱眢绛矧殓徜潋蜚鲮镧ㄦ矧磲铋⑽夏遗刑麸立躞镢脲艉桠锃麸滹趑邃聃徜眢绛矧殓徜潋┅ㄩ忉箦篝狒轱瞽皙痱镧躔溽翦扉铍箦礤篌徵濠躔溽翦漉痨殂狒瀛箦礤篌徵濠┅┅铋飑┅┅ㄤ彐躅箦戾泗轹瀛怛镝溷狍眢绛豉疱眢绛矧殓徜潋⒄铎弩屮痖蝈洮镱禊痱镢弩忉箦篝狒轱忮徙镱骝镯痱弼轱躞禊腩秣箫躜沐ㄩ弪镳ㄨ狍璀翎忪瀛泔躅漉痨殂狒瀛箦舄┅祜镳骘脲忮轭翳栳箬脲轭漉痨殂狒瀛箦舄躞轭ㄨ狍璀鲠祯鲠飑蝈趱蝾ㄩ眢绛豉疱鲠飑眢绛豉疱矧殓徜潋鲠飑眢绛矧殓徜潋舂┅ㄤ彐躅蝈趄殄鲥礤篌徵ㄢ蹑驽箝濠⒄铙弪獒扉抡破乓犷轭麸兴原扰聊乓陀黔扰聊乓犷蕴汁绿厦水嗅蝮陀黔扰聊乓徙泔蜾轭麸移荡创戾舄è疳汶弭躅箦蜷犰辁瀛疳汶弭怩骀弪┅麒孱箝戾铉翳怩骀弪┅换蝈徜箝眭篝磲翥躅箦蜷犰辁邃戾铉翳鏖翳徙沐篌矧è眢绛豉疱眢绛豉疱矧殓徜潋眢绛矧殓徜潋箦癍铛眢绛箦癍铛愆ㄨ镳扉黹眢绛栾瓠扉黹舂ㄨ镳泔躅眢绛栾瓠泔躅舂眢绛桢徜弪礤篌徵疳汶弭┅ㄣ镱è铒礤礅弪眢绛豉疱眢绛豉疱螵┅蜚鲮镧ㄦ矧磲铋⒄我琶锨紊谂再信┅讳轶汜蜾è栾瓠扉黹癌铋飑溟筱狎è栾瓠泔躅驳旦铋飑溟筱狎è栾篝徜潋弩蟓矧殓徜潋蜚鲮镧ㄦ矧磲铋⒃撂松吻韵优唐┅溟筱狎è汨邈氕漉痨殂狒瀛箦眢绛豉疱矧殓徜潋箦癍铛愆蜚鲮镧ㄦ矧磲铋⒛招躺昧耘┅溟筱狎痱镢弩蟓礤篌徵礤篌徵疳汶弭┅┅┅┅ㄤ彐躅秕舡怩骀弪珏ī⒛羼蹂蹂屐屙孱骝镯险原抡破乓犷箦蜷犰辁轸轭麸辛盟旁戾è疳汶弭筲泔钽躜蝈钽轰羼蹂蹂秕舡怩骀弪┅麒孱疳汶弭箦蜷犰辁瀛疳汶弭疳汶弭┅┅ㄤ彐躅蝈痨怩骀弪珏ī⒛羼蹂蹂屐屙孱骝镯遗刑侪抡破乓义趱蝾箦蜷犰辁邃疳汶弭犷溴篝轭狒轱町义趱蝾邃疳汶弭鏖祆忮躅殂狍翦洚戾è蝈痨筲泔钽躜蝈钽轰羼蹂蹂蝈痨怩骀弪┅麒孱蝈痨鲠祯弩箦蜷犰辁瀛疳汶弭蝈痨篝蝓泗疳汶弭蝈痨┅蝈痨篝蝓泗溴篝轭狒轱蝈痨┅┅换糸礤弼孱筱桢漉扉铉ㄤ彐躅汨邈氕漉痨殂狒瀛栾熹轭ī⒁屙秭恼刑擅猎怒优元孱趄殄鏖翳屮痖蝈糸礤篝犴甬祜镳骘脲忮轭翳栳箬脲轭漉痨殂狒瀛箦舄躞轭ㄨ狍璀鲠祯鲠飑麒孱ㄤ艉糸礤窘ㄤ艉铒鳗箪雉鲠祯鲠у瓠糸礤┅滹蝈龛狍脲漉痨殂狒瀛箦舄┅ㄤ彐躅汨邈氕扉铍箦舡鲠扉溟豉ī⒁屙雉躺嗡优元孱趄殄鏖翳屮痖蝈糸礤篝犴甬祜镳骘脲忮轭翳栳箬脲轭扉铍箦舄躞轭ㄨ狍璀鲠祯扉铍趱痨濠麒孱ㄤ艉糸礤窘ㄤ艉铒鳗箪雉鲠祯扉铍趱痨ъ糸礤┅滹痱镧蝈龛狍脲扉铍箦舄换麒孱簌眄弭蜷悱扉铍飙铄殓桠矧殒徙瀛徜潋扉铍趱痨濠ㄤ屐蝻豸轭绛翎忪飙铄殓桠矧殒徙瀛徜潋扉铍趱痨濠┅┅ㄤ彐躅篝狎舡糸礤蝮ī⒂弭躔犷篝狎糸礤蝮筲屮艉筱桢漉戾糸礤筲屮艉磲脲糸礤＇汨邈氕漉痨殂狒瀛栾熹轭侯犴⒛躔扉汜翦渝蚤礤颌呼栩遽舂卑候屦遽舡轭翦蝣犰ㄣ镱骈绛漉瓠栾熹糸礤泔铈殓┅筲屮艉筱桢漉戾糸礤筲屮艉磲脲糸礤＇汨邈氕扉铍箦舡鲠扉溟豉侯犴⑻轭渝蚤礤颌呼栩遽舂卑候屦遽舡轭翦蝣犰ㄣ镱骈绛铄殓桠栾熹糸礤泔铈殓┅筲屮艉筱桢漉戾糸礤筲屮艉磲脲糸礤＇灬礅溽īㄩ忉箦篝狒轱瞽皙铄鳝忮徙镱衡狍瀛篝狒轱瞽忮徙镱铄鳝忮徙镱侯镤瀛忮徙镱┅呼栩遽侯犴⒙遽泔蚤礤颌候屦遽舡轭翦蝣犰ㄣ镱骈绛蝈骝弩璀轭翦蝣犰泔铈殓┅筲屮艉筱桢漉戾糸礤筲屮艉磲脲糸礤＇筱蝈孱呼栩遽舂候屦遽舡轭翦蝣犰ㄣ镱骈绛蝈骝弩璀轭翦蝣犰泔铈殓┅ㄤ彐躅篝镳糸礤蝮īㄤ镬轶糸礤筲屮艉扉篝犰飙糸礤蝮┅筲屮艉躅筱桢漉戾糸礤糸礤颟┅换韵南牦眇弪彘蜥犁磲殪泔愆梏麴函麸镬螽殄翩矧绡梏盱蜴愕贝书趑弪蔑铙殇弪狒轱铙轭土闻泽ㄤ彐躅觊趑弪糸礤⒘滗箫礤铒轶麸陨团ㄤ艉箦泔钿糸礤ō蜥钿镯ㄦ祜狒磲觊趑弪┅┅换溴怩ㄤ彐躅痱轭舡栳箬ㄨ狍瑭祜镳骘忮轭翳栳箬脲轭栳箬躞轭ㄨ狍璀鲠祯雯泔祆邈ㄦ矧磲铋⑺湖趾窿ア雯┅ㄤ彐躅筱蝈孱ī鏖翳镳孱骈戾礤蜱瀛疳翳钺礤Ⅲ泸邋睥躞弪栾礤溟颦疳翳钺礤┅轰轵邈糸镱猴豸瘐洪姝屮轶趔后躔弪箦溴ㄦ矧磲绎豸轭葬忪窿孽痨殂狒渝窿涕铍渝窿险抡破乓窿窿遗刑抡破乓窿ア痱轭舡栳箬蝻豸轭绛翎忪濯痱轭舡栳箬漉痨殂狒瀛箦舄痱轭舡栳箬扉铍箦舄筲泔钽躜蝈钽红轶舡聃艴瀛泔铘孱趔秕舡怩骀弪筲泔钽躜蝈钽厚蹂蹂泔躅秕舡怩骀弪筲泔钽躜蝈钽红轶舡聃艴瀛泔铘孱趔蝈痨怩骀弪┅┅ㄤ彐躅蜚鲮镧é蝈篝蝈篝鏖翳镳孱骈戾礤蜱瀛疳翳钺礤Ⅱ邈彘鲥洧躞弪栾礤溟颦疳翳钺礤┅轰轵邈糸镱猴豸瘐洪姝屮轶趔横痧孱洎ㄦ矧磲%" rest)))

;;; util

(defmacro with-hash ((hash k v) &body body)
  `(loop for ,k being the hash-keys in ,hash using (hash-value ,v)
	 ,@body))

(defun load-config (&optional (path "quicklisp/local-projects/chopin-routing/.config"))
  (with-open-file (in (merge-pathnames path (user-homedir-pathname)) :direction :input)
    (let ((conf (read in)))
      (setf *config* (apply #'make-config conf))))
  (setf *max-jitter* (/ (config-refresh-interval *config*) 4)))

(defun valid-msg-type-p (msg-type)
  (getf *msg-types* msg-type))

(defun host-address-p (orig-addr)
  "Return T if ORIG-ADDR equals current node address. Otherwise return NIL."
  (string= (usocket:hbo-to-dotted-quad orig-addr) (config-host-address *config*)))

(defun kernel-table-cleanup ()
  "Loop through *ROUTING-TABLE* and cleanup routing entries from Kernel IP table."
  #-darwin
  (with-hash (*routing-table* k v)
    do (del-route (rt-entry-destination v) "0" (config-interface *config*) (rt-entry-hop-count v))))
